
configfile: "workflow/config/config.yaml"

# path configuration
rawdata = Path(config["rawdataDir"])
metadata = Path(config["metadataDir"])
procdata = Path(config["procdataDir"])
results = Path(config["resultsDir"])
scripts = Path(config["scriptsDir"])


rule build_PharmacoSet:
    input:
        results / "data/ExpressionGenes_SE.qs",
        results / "data/ExpressionTranscripts_SE.qs",
        procdata / "metadata/annotatedSampleData.qs",
        procdata / "metadata/annotatedTreatmentData.qs",
        results / "data/treatmentResponseExperiment.qs",
        rawdata / "mutation/CCLE_Oncomap3_Assays_2012-04-09.csv",
        rawdata / "mutation/CCLE_Oncomap3_2012-04-09.maf",
        rawdata / "methylation/CCLE_RRBS_TSS1kb_20181022.txt.gz",
    output:
        results / "data/CCLE_PSet.qs"
    log:
        "logs/build_PharmacoSet.log"
    threads:
        4
    script:
        scripts / "build_PharmacoSet.R"

rule make_CNV_SE:
    input:
        procdata / "preprocessedCNV.qs",
    output:
        results / "data/CNV_SE.qs"
    script:
        scripts / "expression/make_ExpressionCNV_SE.R"
    
rule make_ExpressionGenes_SE:
    input:
        preprocessedExpression = procdata / "preprocessedExpressionGenes.qs",
    output:
        processedExpressionGenesSE = results / "data/ExpressionGenes_SE.qs"
    threads:
        4
    script:
        scripts / "expression/make_ExpressionGenes_SE.R"

rule make_ExpressionTranscripts_SE:
    input:
        preprocessedExpression = procdata / "preprocessedExpressionTranscripts.qs",
    output:
        processedExpressionTranscriptsSE = results / "data/ExpressionTranscripts_SE.qs"
    threads:
        4
    script:
        scripts / "expression/make_ExpressionTranscripts_SE.R"


# All other rules can be found in the rules directory at these locations:
include: "rules/preprocessData.smk"
include: "rules/processMetadata.smk"
include: "rules/processTreatmentResponse.smk"